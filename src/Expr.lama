-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Assn (x, e) ->
      case evalList (c, {x, e}) of
        [[s, w], Ref (x) : v : _] -> [[s <- [x, v], w], v]
      esac
    | Seq (e1, e2) ->
      case evalList(c, {e1, e2}) of
        [c, {_, v2}] -> [c, v2]
      esac
    | Skip -> [c, {}]
    | Read (x) ->
      case readWorld(w) of
        [v, w] -> [[s <- [x, v], w], {}]
      esac
    | Write (e) ->
      case eval(c, e) of
        [[c, w], v] -> [[c, writeWorld(v, w)], {}]
      esac
    | If (cond, thenBranch, elseBranch) ->
      case eval(c, cond) of
        [c, v] -> if v
                  then eval (c, thenBranch)
                  else eval (c, elseBranch)
                  fi
      esac
    | While (cond, body) ->
      case eval(c, cond) of
        [c, v] -> if v
                  then eval (c, Seq(body, expr))
                  else [c, {}]
                  fi
      esac
    | DoWhile (body, cond) ->
      case eval(c, body) of
        [c, _] -> eval(c, While(cond, body))
      esac
    | Var (x) -> [c, s(x)]
    | Ref (x) -> [c, Ref(x)]
    | Const (v) -> [c, v]
    | Binop (op, e1, e2) ->
      case eval(c, e1) of
        [c, value1] ->
          case eval (c, e2) of
            [c, value2] -> [c, evalOp(op, value1, value2)]
          esac
      esac
    | Ignore (e) ->
      case eval(c, e) of
        [c, _] -> [c, {}]
      esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
